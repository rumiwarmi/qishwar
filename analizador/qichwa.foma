### qichwa-new.foma ###

read lexc qichwa-new.lexc
define Lexicon;

define C [c h|k|l|l l|m|n|ñ|p|q|r|s|s h|t|t s];

define V [a|i|u];

define Vlong [a a | i i | u u];

define Semi [w|y];

define closedSyl [r q | r p | p t | y k | r k | n k | n m | s h q | [n | q | r | y | m] [.#. | "^"] ];

define FirstPers "&" -> a || a "^" _ ,,
						"&" -> u || u "^" _ ,,
						"&" -> i || i "^" _ ,,
						"&" -> n i i || [Vlong | Semi | C] "^" _ ;
						
#----------------#
# EUPHONIC RULES #
#----------------#

# rkU, rpU, ku se convierte en rka, rpa, ka delante -tsi / -pu / -mu / -maA
define Euphony u -> a || "^" [r p | r k | k | y k]  _ "^" (?* "^") [m u | p u | m a | t s i] ;

# El aplicativo -pU se convierte en -pa delante de -tsi, -mu o -maA
define EuphonyPu p u -> p a || "^" _ "^" (?* "^") [t s i | m u | m a] ;


# La eufonía se aplica también a verbos cuya raíz contiene esos sufijos : qarqu, qarpu, qarku ; yarku, yarqu, yarpu (la lista queda por completar)
define EuphonyRoot u -> a || [y | q] a [r q | r p | r k ] _ "^" (?* "^") [m u | p u | m a | t s i] ;


# Los sufijos -naA, -ykachaA, -kachaA, -paA, -kaA, -raA, -ykaA, -llaA, -yaA et -maA pierden la duración vocálica ante sílabas cerradas
define VowelShortening a a -> a || "^" [n | y k a c h | k a c h | p | k | r | y k | l l | y | m ] _ "^" closedSyl ;

# La reducción vocálica se observa a veces delante de -mu, en general despues de una vocal larga  (shayamu-, chaayamu-, yarqaraykaayamunnam, ...)
define VowelShorteningYaa a a (->) a || "^" y  _ (?* "^") m u ;
define ShorteningShaa a a -> a || .#. s h _ "^" y a ;


# La reducción vocálica aparece también en las raices verbales shaa-, chaa-, rikaa-, kachakaa-, mantsakaa- (la lista queda por completar)
define RootShortening a a -> a || .#. [ c h | s h | r i k | k a c h a k | m a n t s a k ] _ "^" closedSyl ;


# El sufijo -chuu (variante de -chaw) pierde su duración en final de palabra
define VowelShorteningChuu	u u -> u || "^" c h _ .#. ;

# El pronombre "qam" se convierte en "qan" ante los sufijos -mi y -man
define QamQan m -> n || .#. q a _ "^" m [i | a n] .#. ;

#------------#
# Epenthesis #
#------------#
define Epenthesis [..] -> n i || [C | Vlong | Semi] _ "^" [n | n t s i k | y k i] ["^" | .#.] ;


#------------------------#
# Evidentials allomorphy #
#------------------------#

define FinalI i (->) 0 || C V "^" [m | s h | c h ] _ .#. ;



#------------ COMPILATION ---------------#
# Cleanup: remove morpheme boundaries
define Cleanup "^" -> 0;


define Grammar  Lexicon	.o.
				FirstPers .o.
				Euphony .o.
				EuphonyPu .o.
				EuphonyRoot .o.
				VowelShortening .o.
				VowelShorteningYaa .o.
				ShorteningShaa .o.
				RootShortening .o.
				VowelShorteningChuu .o.
				QamQan .o.
				Epenthesis .o.
				FinalI .o.
				Cleanup;

regex Grammar;
