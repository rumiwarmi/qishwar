### qichwa-guess.foma ###

define C [b | c h | d | k | l | l l | m | n | ñ | p | q | r | s | s h | t |t s];
define V [a | i | u];
define CEsp [b | d | f];
define VEsp [e | o];
define Vlong [a a | i i | u u];
define Semi [w|y];

define OrthWord [ [C|Semi] V [C|Semi] V]; ![ C* [V+|Vlong] C* C+ [V+ C* | Vlong] ];
read lexc qichwa-guess.lexc
substitute defined OrthWord for "^GUESS"
define Lexicon;


define closedSyl [r q | r p | p t | y k | r k | n k | n m | s h q | t s | [n | q | r | y | m] [.#. | "^"] ];

define FirstPers "&" -> a || a "^" _ ,,
						"&" -> u || u "^" _ ,,
						"&" -> i || i "^" _ ,,
						"&" -> n i i || [Vlong | Semi | C] "^" _ ;
						
#----------------#
# EUPHONIC RULES #
#----------------#

# rkU, rpU, ku se convierte en rka, rpa, ka delante -tsi / -pu / -mu / -maA
define Euphony u -> a || "^" [r p | r k | k | y k]  _ "^" (?* "^") [m u | p u | m a | t s i] ;

# El aplicativo -pU se convierte en -pa delante de -tsi, -mu o -maA
define EuphonyPu p u -> p a || "^" _ "^" (?* "^") [t s i | m u | m a] ;



# Los sufijos -naA, -ykachaA, -kachaA, -paA, -kaA, -raA, -ykaA, -llaA, -yaA et -maA pierden la duración vocálica ante sílabas cerradas

define VowelShortening a a -> a || "^" [n | y k a c h | k a c h | p | k | r | y k | l l | y | m ] _ "^" closedSyl ;

define ReducLlaa a a -> a || l l _ "^" [ Evidential | .#. ];


# El sufijo -chuu (variante de -chaw) pierde su duración en final de palabra

define VowelShorteningChuu	u u -> u || "^" c h _ .#. ;




#------------#
# Epenthesis #
#------------#
define Epenthesis [..] -> n i || [C | Vlong | Semi] _ "^" [n | n t s i k | y k i] ["^" | .#.] ;


#------------------------#
# Evidentials allomorphy #
#------------------------#

define FinalI i (->) 0 || C V "^" [m | s h | c h ] _ .#. ;


# Non standard spelling

define Labialisation n (->) m || "^" _ "^" p ;

define Sandhi2  y (->) 0 || i "^" _ k ;


# Cleanup: remove morpheme boundaries
define Cleanup "^" -> 0;

define Grammar  Lexicon	.o.
				!FirstPers .o.
				Euphony .o.
				EuphonyPu .o.
				VowelShortening .o.
				ReducLlaa .o.
				VowelShorteningChuu .o.
				Epenthesis .o.
				FinalI .o.
				Labialisation .o.
				Sandhi2 .o.
				Cleanup;

regex Grammar;

